set terminal postscript enhanced eps color lw 1 16 font "Times-Roman"
set output "./FIGS/forwardTrafo.eps"


LW=1.
PS=1.
set style line 1 lw LW lt 1 ps PS  pt 4 lc rgb "black" dashtype 1 
set style line 2 lw LW lt 1 ps PS  pt 5 lc rgb "red" dashtype '-' 
set style line 3 lw LW lt 1 ps PS  pt 6 lc rgb "blue" dashtype '-.'
set style line 4 lw LW lt 1 ps PS  pt 7  lc rgb "blue" dashtype '-..'
set style line 5 lw LW lt 1 ps PS  pt 8  lc rgb "blue" dashtype '.'

set size 0.72,1.
set tics nomirror
set multiplot

set label 11 "{/Times-Roman (a)}" at screen 0.02,0.98 center
set tmargin at screen 0.98
set bmargin at screen 0.65
set lmargin at screen 0.1
set rmargin at screen 0.68
set xr [0:9]
set yr [-0.15:1.15]
set ytics (0,0.2,0.4,0.6,0.8,1.0,1.2)
set xtics (0,2,4,6,8,"{/Symbol r}" 9)
set xzeroaxis

set key samplen 1.2 reverse Left at graph 0.37,0.6
set label 12 "{/Times-Italic N} = 20" at graph 0.05,0.73 
set label 13 "{/Times-Italic F}_{/Times-Roman=12 0}({/Symbol r})" at screen 0.02,0.775 rotate by 90
#set label 14 "{/Symbol r}" at screen 0.63,0.62

p '<grep "O " ../data_sombrero/dFBT_fourierPair_sombrero_T20.0_N20.dat' u 2:3 w l ls 1 t "exact"\
, '<grep "CB " ../data_sombrero/dFBT_fourierPair_sombrero_T20.0_N20.dat' u 2:3 w l ls 2 t "CB"\
, '<grep "FJCEx " ../data_sombrero/dFBT_fourierPair_sombrero_T4.0_N20.dat' u 2:3 w l ls 3 t "FJc-{/Times-Italic T} = 4.0"\
, '<grep "FJCEx " ../data_sombrero/dFBT_fourierPair_sombrero_T8.0_N20.dat' u 2:3 w l ls 4 t "FJc-{/Times-Italic T} = 8.0"\
, '<grep "FJCEx " ../data_sombrero/dFBT_fourierPair_sombrero_T20.0_N20.dat' u 2:3 w l ls 5 t "FJc-{/Times-Italic T} = 20.0"

unset label 11
unset label 12
unset label 13



set tmargin at screen 0.96
set bmargin at screen 0.75
set lmargin at screen 0.4
set rmargin at screen 0.66
set xr [0:20]
set yr [0.07:0.8]
set ytics (0,0.1,0.2,0.4,"{/Symbol e}_{/=10 RMS}" 0.8)
set xtics (0,5,10,15,"{/Times-Italic T}" 20)
set key samplen 1.2 at graph 1.2,0.92
set logs y

p '../data_sombrero/dFBT_FWDTrafo_eRMS_sombrero_5.dat' u 1:4 w l ls 1 dashtype '.' t "FJc - {/Times-Italic N} = 5"\
, '../data_sombrero/dFBT_FWDTrafo_eRMS_sombrero_40.dat' u 1:4 w l ls 1 dashtype 1 t "FJc - {/Times-Italic N} = 40 "\
, '../data_sombrero/dFBT_FWDTrafo_eRMS_sombrero_5.dat' u 1:3 w l ls 2 dashtype  '-' t "CB"

unset logs y



set label 31 "{/Times-Roman (b)}" at screen 0.02,0.58 center
set tmargin at screen 0.58
set bmargin at screen 0.25
set lmargin at screen 0.1
set rmargin at screen 0.68
set xr [0:2.1]
set yr [-0.5:6.5]
set ytics (0,1,2,3,4,5,6)
set xtics (0,0.5,1,1.5,2,"{/Symbol r}" 2.1)
set xzeroaxis

set key samplen 1.2 reverse Left at graph 0.37,0.56
set label 32 "{/Times-Italic N} = 20" at graph 0.14,0.9 
set label 33 "{/Times-Italic F}_{/Times-Roman=12 0}({/Symbol r})" at screen 0.02,0.375 rotate by 90
#set label 14 "{/Symbol r}" at screen 0.63,0.62

p '<grep "O " ../data_Gauss/dFBT_fourierPair_Gauss_T10.0_N20.dat' u 2:3 w l ls 1 t "exact"\
, '<grep "CB " ../data_Gauss/dFBT_fourierPair_Gauss_T10.0_N20.dat' u 2:3 w l ls 2 t "CB"\
, '<grep "FJCEx " ../data_Gauss/dFBT_fourierPair_Gauss_T4.0_N20.dat' u 2:3 w l ls 3 t "FJc-{/Times-Italic T} = 4.0"\
, '<grep "FJCEx " ../data_Gauss/dFBT_fourierPair_Gauss_T6.0_N20.dat' u 2:3 w l ls 4 t "FJc-{/Times-Italic T} = 6.0"\
, '<grep "FJCEx " ../data_Gauss/dFBT_fourierPair_Gauss_T10.0_N20.dat' u 2:3 w l ls 5 t "FJc-{/Times-Italic T} = 10.0"

unset label 31
unset label 32
unset label 33


set tmargin at screen 0.56 
set bmargin at screen 0.35
set lmargin at screen 0.4
set rmargin at screen 0.66
set xr [0:10]
set yr [0.0001:10000.]
set ytics (10**(-4),10**(-2),1,10**2,"{/Symbol e}_{/=10 RMS}" 10**4)
set xtics (0,2,4,6,8,"{/Times-Italic T}" 10)
set key samplen 1.2 at graph 1.15,0.92
set logs y
set format y "10^{%L}"

p '../data_Gauss/dFBT_FWDTrafo_eRMS_Gauss_5.dat' u 1:4 w l ls 1 dashtype '.' t "FJc - {/Times-Italic N} = 5"\
, '../data_Gauss/dFBT_FWDTrafo_eRMS_Gauss_20.dat' u 1:4 w l ls 1 dashtype 1 t "FJc - {/Times-Italic N} = 20"\
, '../data_Gauss/dFBT_FWDTrafo_eRMS_Gauss_20.dat' u 1:3 w l ls 2 dashtype  '-' t "CB"

unset multiplot
