set terminal postscript enhanced eps color lw 1 16 font "Times-Roman"
set output "./FIGS/reverseTrafo.eps"


LW=1.
PS=1.
set style line 1 lw LW lt 1 ps PS  pt 4 lc rgb "black" dashtype 1 
set style line 2 lw LW lt 1 ps PS  pt 5 lc rgb "red" dashtype '-' 
set style line 3 lw LW lt 1 ps PS  pt 6 lc rgb "blue" dashtype '-.'
set style line 4 lw LW lt 1 ps PS  pt 7  lc rgb "blue" dashtype '-..'
set style line 5 lw LW lt 1 ps PS  pt 8  lc rgb "blue" dashtype '.'

set size 0.72,1.
set tics nomirror
set multiplot

set tmargin at screen 0.98
set bmargin at screen 0.65
set lmargin at screen 0.1
set rmargin at screen 0.68
set xr [0:10]
set yr [-1.:5]
set ytics (-1,0,1,2,3,4,5,6)
set xtics (0,2,4,6,8,10,"{/Times-Italic r}" 10)
set xzeroaxis

set key samplen 1.2 at graph 0.37,0.98
set label 12 "{/Times-Italic T} = 10.0" at graph 0.22,0.35 
set label 13 "{/Times-Italic f_{/Times-Roman=10 rec}(r)}" at screen 0.02,0.775 rotate by 90

p '<grep "FJB " ../data_sombrero/dFBT_fourierPair_sombrero_T10.0_N5.dat' u 2:3 w l ls 1 t "{/Times-Italic f(r)}"\
, '<grep "FJB " ../data_sombrero/dFBT_fourierPair_sombrero_T10.0_N5.dat' u 2:4 w l ls 3 dashtype '-' t "{/Times-Italic N} = 5"\
, '<grep "FJB " ../data_sombrero/dFBT_fourierPair_sombrero_T10.0_N8.dat' u 2:4 w l ls 3 dashtype '-.' t "8"\
, '<grep "FJB " ../data_sombrero/dFBT_fourierPair_sombrero_T10.0_N10.dat' u 2:4 w l ls 3 dashtype '-..' t "10"\
, '<grep "FJB " ../data_sombrero/dFBT_fourierPair_sombrero_T10.0_N40.dat' u 2:4 w l ls 3 dashtype '.' t "20"

unset label 12
unset label 13



set tmargin at screen 0.96
set bmargin at screen 0.765
set lmargin at screen 0.4
set rmargin at screen 0.66
set xr [0:40]
set yr [0.0006:200.]
set ytics (0.001,0.1,10,"{/Symbol e}_{/=10 RMS}" 200)
set xtics (0,10,20,30,"{/Times-Italic N}" 40)
set key samplen 1.2 at graph 0.98,0.95
set logs y
set format y "10^{%L}"

p '../data_sombrero/dFBT_FWDTrafo_eRMS_sombrero_varyN_T4.0.dat' every 2 u 2:5 w lp ls 1 pt 13 ps 1. lw 0.8 t "{/Times-Italic T} = 4.0"\
, '../data_sombrero/dFBT_FWDTrafo_eRMS_sombrero_varyN_T10.0.dat' every 2 u 2:5 w lp ls 1  pt 9 ps 1. lw 0.8 t "10.0"\
, '../data_sombrero/dFBT_FWDTrafo_eRMS_sombrero_varyN_T20.0.dat' every 2 u 2:5 w lp ls 1  pt 7 ps 1. lw 0.8 t "20.0"



unset multiplot
